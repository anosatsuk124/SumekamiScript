Sumekami = _{ SOI ~ Sexpr* ~ EOI }

Sexpr = { List | Literal | Lambda | Operator }

List = { "(" ~ Sexpr* ~ ")" }

Lambda = { ImmutableKeyword? ~ Symbol ~ TypeAnnotation? }
TypeAnnotation = { ":" ~ Symbol }
ImmutableKeyword = { "!" }

Literal = { String | Char | Number }

String =  _{ PUSH(DoubleQuote) ~ StringValue ~ POP }
StringValue = { (!PEEK ~ ANY)* }
DoubleQuote = _{ "\"" }

Char =  _{ PUSH(SingleQuote) ~ CharValue ~ POP }
CharValue = { ANY }
SingleQuote = _{ "\'" }

Symbol = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

Number = { HexNumber | OctNumber | BinNumber | DecNumber }
HexNumber = @{ "0x" ~ ASCII_HEX_DIGIT+ }
OctNumber = @{ "0o" ~ ASCII_OCT_DIGIT+ }
BinNumber = @{ "0b" ~ ASCII_BIN_DIGIT+ }
DecNumber = @{ ASCII_DIGIT+ }

Operator = { PtrOperator | RefOperator }
PtrOperator = { "*" }
RefOperator = { "&" }

COMMENT = _{ COMMENT_MARKER ~ COMMENT_CONTENT ~ NEWLINE}
COMMENT_CONTENT = { (!NEWLINE ~ ANY)* }
COMMENT_MARKER = { ";" }
WHITESPACE = _{ " " | "\t" | NEWLINE }